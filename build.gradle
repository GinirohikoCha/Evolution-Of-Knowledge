buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
	jcenter()
    maven { url = 'https://files.minecraftforge.net/maven' }
	maven { url = 'https://zi-jing.github.io/cuckoo-maven/maven' }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

// 在这里更改版本
version = '1.0.1.10-alpha'
group = 'com.gonggongjohn.eok'
archivesBaseName = 'eok'

// 添加前缀后缀
String mcVersion = '1.12.2'
version = mcVersion + '-' + version + getVersionAppendage()

sourceCompatibility = targetCompatibility = '1.8' // 使"eclipse" task正确生成启动方案
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

static String getVersionAppendage() {
    def proc = 'git rev-parse --short HEAD'.execute()
    proc.waitFor()
    return '-' + (proc.exitValue() ? 'NO-GIT' : proc.text.trim())
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.main.output
    from sourceSets.main.allSource
    include 'com/gonggongjohn/eok/api/**'
}

artifacts {
    //noinspection GrUnresolvedAccess
    archives apiJar
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'

    mappings = 'snapshot_20171003'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    // 替换EOK版本号
	replaceIn 'EOK.java'
	replace '@EOKVERSION@', project.version
}

dependencies {
	compile 'com.osir.tmc:tmc-api:1.3.2.1'
    compile 'com.github.zi_jing.cuckoolib:CuckooLib:1.0.4'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// 强制UTF-8构建
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}
